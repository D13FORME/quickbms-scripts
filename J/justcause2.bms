# Just Cause 2 (version 0.1.3)
# script for QuickBMS http://quickbms.aluigi.org

# useful for some files ("SARC") compressed with zlib
comtype unzip_dynamic

open FDDE "tab" 0
open FDDE "arc" 1

set x360 long 0
get ARC_SIZE asize 1

get TAB_SIZE asize
get PAD_SIZE long   # 0x800
if PAD_SIZE >= 0x80000
    set x360 long -1
    endian big
    goto 0
    get PAD_SIZE long
endif
if PAD_SIZE != 0x800
    get PAD_SIZE long
    get DUMMY long
endif

math OFFSET_WORKAROUND = 0
for i = 0
    savepos CURR_OFF
    if CURR_OFF >= TAB_SIZE
        cleanexit
    endif
    get NAME_CRC long
    get OFFSET long
    get SIZE long
    if x360 == -1
        if OFFSET != 0
            if OFFSET == OFFSET_WORKAROUND
                math x360 = 0
            else
                math x360 = 1
            endif
        endif
    endif
    if x360 != 0
        math OFFSET *= PAD_SIZE
    endif

    math TMP = OFFSET
    math TMP += SIZE
    if TMP u<= ARC_SIZE # unsigned comparison, work-around for x360
        callfunction COOL_DUMP 1
        
        # quickbms now has its own extensions generator
        # but COOL_DUMP is more specific for this game
        #clog "" OFFSET SIZE SIZE 1
    endif
    
    if i == 0
        math OFFSET_WORKAROUND = OFFSET
        math OFFSET_WORKAROUND += SIZE
        math OFFSET_WORKAROUND x= PAD_SIZE
    endif
next i

startfunction COOL_DUMP
    goto OFFSET 1
    getdstring SIGN 4 1
    goto OFFSET 1
    get SIGN1 byte 1
    goto OFFSET 1
    getdstring SIGN2 2 1
    goto OFFSET 1
    get SIGN4 long 1
    if SIGN == "DDS "
        set EXT string "dds"
    elif SIGN == "WBND"
        set EXT string "xwb"
    elif SIGN == "DNBW"
        set EXT string "xwb"
    elif SIGN == "RIFF"
        set EXT string "wav"
    elif SIGN == "FEV1"
        set EXT string "fev"
    elif SIGN == "FSB4"
        set EXT string "fsb"
    elif SIGN == "WààW"
        set EXT string "WaaW"
    elif SIGN == "VER"
        set EXT string "ver"
    elif SIGN4 == 4
        set EXT string "sarc"
    elif SIGN4 == 5
        set EXT string "rbmd"
    elif SIGN4 == 12
        set EXT string "12"
    elif SIGN2 == "BM"
        set EXT string "bmp"
    elif SIGN2 == "//"
        set EXT string "psh"
    elif SIGN1 == 0x3c
        set EXT string "xml"
    elif SIGN1 == 0x78
        set EXT string "offzip"
    elif SIGN1 == 0x01
        set EXT string "obj"
    else
        #set EXT string "dat"        
        string EXT f= SIGN
    endif
    
    #set NAME string NAME_CRC
    #string NAME += EXT
    string NAME p= "%08x.%s" NAME_CRC EXT
    clog NAME OFFSET SIZE SIZE 1
endfunction
