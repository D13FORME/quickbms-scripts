# Smart Install Maker EXE to CAB
#   supports also password-protected archives without knowing the keyword.
#   compressed installers (default) are converted in CAB archives while
#   the uncompressed ones are extracted directly
# script for QuickBMS http://quickbms.aluigi.org

get PAK_NAME basename

findloc OFFSET string "Smart Install Maker v"
goto OFFSET
get VER string

math PAK_SIZE = 0
set MULTI_PATTERN string ""
math ONE_VOLUME = 0
math FILES = 0
math CAB_SIZE = 0

for i = 0 < 120
    get TMP string
      if i == 22
        set PAK_SIZE long TMP
    elif i == 20
        set MULTI_PATTERN string TMP
    elif i == 33
        set ONE_VOLUME long TMP
    elif i == 66
        set FILES long TMP
    elif i == 117
        set CAB_SIZE long TMP
    endif
next i

print "PAK_SIZE      %PAK_SIZE%"
print "MULTI_PATTERN %MULTI_PATTERN%"
print "ONE_VOLUME    %ONE_VOLUME%"
print "FILES         %FILES%"
print "CAB_SIZE      %CAB_SIZE%"

for i = 0 < FILES
    get NAME string
    string NAME R= @$&%04\ ""
    putarray 0 i NAME
    get TMP string
    get TMP string
next i

goto -0x24
get INFO_OFFSET longlong
get INST_SIZE longlong
get INST_OFFSET longlong
get OFFSET longlong
get TYPE long
goto OFFSET

if TYPE & 1
    get DUMMY long
    get CAB_SIZE long
    math CAB_SIZE -= 4

    log MEMORY_FILE 0 0
    putdstring "MSCF" 4 MEMORY_FILE

    append
    if ONE_VOLUME != 0
        log MEMORY_FILE OFFSET CAB_SIZE
    else
        math i = 2
        for XSIZE = 0 < CAB_SIZE
            set TMP string MULTI_PATTERN
            string TMP R= "@$&%19" i
            open FDSE TMP 1
            get TMP asize 1
            log MEMORY_FILE 0 TMP 1
            math XSIZE += TMP
        next i
    endif
    append
    math CAB_SIZE += 4

    # replace placeholders with real filenames
    goto 0x24 MEMORY_FILE
    get DATA_OFF long MEMORY_FILE
    math CAB_SIZE -= DATA_OFF
    log MEMORY_FILE2 0 0x2c MEMORY_FILE
    goto 0x2c MEMORY_FILE
    goto 0x2c MEMORY_FILE2
    append
    for i = 0 < FILES
        getdstring INFO 0x10 MEMORY_FILE
        get TMP string MEMORY_FILE
        getarray NAME 0 i
        putdstring INFO 0x10 MEMORY_FILE2
        put NAME string MEMORY_FILE2
    next i
    savepos TMP MEMORY_FILE2
    putvarchr MEMORY_FILE2 0x24 TMP long
    log MEMORY_FILE2 DATA_OFF CAB_SIZE MEMORY_FILE
    append
    math CAB_SIZE += TMP

    # dump CAB file
    string PAK_NAME += ".cab"
    log PAK_NAME 0 CAB_SIZE MEMORY_FILE2
    print "\nSuccess: open %PAK_NAME% with 7-zip or any other program for CAB files"

else

    for i = 0 < FILES
        get DUMMY long
        get SIZE long
        getdstring DUMMY 0x10
        savepos OFFSET

        getarray NAME 0 i
        set FNAME string PAK_NAME
        string FNAME += /
        string FNAME += NAME

        log FNAME OFFSET SIZE
        math OFFSET += SIZE
        goto OFFSET
    next i 

endif
