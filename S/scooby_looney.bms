# Scooby Doo & Looney Tunes Cartoon Universe: Adventure (DATA and VOL)
#   maybe other games of WayForward Technologies
# script for QuickBMS http://quickbms.aluigi.org

ComType deflate

get SIGN long

if SIGN == 0x48a6647d
    get DUMMY long
    get DUMMY1_OFF long
    get FILES long
    get NAMES_OFF long
    get INFO_OFF long
    get OFFSETS_OFF long
    get DATA_OFF long

    goto NAMES_OFF
    for i = 0 < FILES
        get NAME_OFF long
        putarray 0 i NAME_OFF
    next i

    goto INFO_OFF
    for i = 0 < FILES
        get CRC long
        get ZIP long
        get ZSIZE longlong
        get SIZE longlong
        putarray 1 i ZIP
        putarray 2 i ZSIZE
        putarray 3 i SIZE
    next i

    goto OFFSETS_OFF
    for i = 0 < FILES
        get OFFSET longlong
        putarray 4 i OFFSET
    next i

    for i = 0 < FILES
        getarray NAME_OFF 0 i
        getarray ZIP 1 i
        getarray ZSIZE 2 i
        getarray SIZE 3 i
        getarray OFFSET 4 i

        goto NAME_OFF
        get NAME string
        string NAME R= "_" "/"
        if ZIP == 0
            log NAME OFFSET SIZE
        else
            clog NAME OFFSET ZSIZE SIZE
        endif
    next i

elif SIGN == 0xb53d32cb
    get DUMMY short # 2
    get DUMMY short # 0 or 4
    get DUMMY long  # 0x1c
    get FILES long
    get INFO_OFF long
    get DATA_OFF long

    goto INFO_OFF
    for i = 0 < FILES
        get CRC long
        get NAME_OFF long
        get OFFSET longlong
        get SIZE long

        savepos TMP
        goto NAME_OFF
        get NAME string
        goto TMP
        string NAME R= "_" "/"
        log NAME OFFSET SIZE
    next i

else
    print "Error: unsupported file type %SIGN|x%"
    cleanexit
endif
