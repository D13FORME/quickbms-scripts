# Sonic 2006
# script for QuickBMS http://quickbms.aluigi.org

endian big
idstring "\x55\xaa\x38\x2d"
get OFFSET long
get INFO_SIZE long
get BASE_OFFSET long
getdstring DUMMY 0x10
goto OFFSET
set NAME_BASE long 0
set PATH string ""
set NAME string ""
math XLIMIT = 0x7fffffff
putarray 0 0 0
callfunction EXTRACT

startfunction EXTRACT
    string PATH += NAME
    string PATH += /
    math LIMIT = XLIMIT
    for
        getarray i 0 0
        if i >= LIMIT
            break
        endif
        math i += 1
        putarray 0 0 i

        get FOLDER short
        get NAME_OFF short
        if FOLDER == 0
            get OFFSET long
            get ZSIZE long
            get SIZE long
        else
            get ID long
            get XLIMIT long
            get DUMMY long
        endif

        if NAME_BASE == 0
            math NAME_BASE = XLIMIT
            math NAME_BASE *= 0x10
            math INFO_SIZE -= NAME_BASE
            math NAME_BASE += OFFSET
            log MEMORY_FILE NAME_BASE INFO_SIZE
            math LIMIT = XLIMIT
        endif
        goto NAME_OFF MEMORY_FILE
        get NAME string MEMORY_FILE

        if FOLDER == 0
            set FNAME string PATH
            string FNAME += NAME
            clog FNAME OFFSET ZSIZE SIZE
        else
            callfunction EXTRACT
        endif
    next
endfunction
