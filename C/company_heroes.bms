# Company of Heroes
#   archive similar to Warhammer 40000
# script for QuickBMS http://quickbms.aluigi.org

idstring "_ARCHIVE"
get VERSION long
getdstring DUMMY 0x10
getdstring NAME 0x80
set NAME unicode NAME
getdstring DUMMY 0x10
get INFO_SIZE long
get BASE_OFF long
get DUMMY long      # 1
savepos INFO_OFF
get DUMMY long      # 0x18
get DUMMY short     # 1
get HEAD_SIZE long  # 0xa2
get DUMMY short     # ???
get TREE_SIZE long
get FILES short

math INFO_OFF += TREE_SIZE
goto INFO_OFF

math TMP = FILES
math TMP *= 22
math TMP += INFO_OFF
math SIZE = BASE_OFF
math SIZE -= TMP
log MEMORY_FILE2 TMP SIZE

for i = 0 < FILES
    get NAME_OFF long
    get OFFSET long
    get ZSIZE long
    get SIZE long
    get CRC long
    get ZERO byte
    get ZIP byte

    goto NAME_OFF MEMORY_FILE2
    get NAME string MEMORY_FILE2

    math OFFSET += BASE_OFF
    if ZIP == 0
        log NAME OFFSET SIZE
    else
        clog NAME OFFSET ZSIZE SIZE
    endif
next i
