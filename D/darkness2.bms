# Darkness 2 (script 0.2)
#   derived from GMMan's Evolution Engine Extractor
# script for QuickBMS http://quickbms.aluigi.org

quickbmsver "0.5.25a"
comtype evolution

open FDDE "toc"
open FDDE "cache" 1

idstring "\x4e\xc6\x67\x18"
get VER long
get FILES asize
math FILES -= 8
math FILES /= 0x60

math nextDirIndex = 0
putarray 6 nextDirIndex ""
math nextDirIndex += 1
math ENTRIES = 0

# pre-allocate arrays
for i = 0 <= 6
    putarray i FILES ""
next i

for i = 0 < FILES
    get OFFSET longlong
    get TSTAMP longlong
    get ZSIZE long
    get SIZE long
    get RESERVED long
    get ID long
    getdstring NAME 0x40

    if OFFSET == 0xffffffffffffffff
        getarray PATH 6 ID
        string PATH += /
        string PATH += NAME
        putarray 6 nextDirIndex PATH
        math nextDirIndex += 1
    else
        putarray 1 ENTRIES NAME
        putarray 2 ENTRIES OFFSET
        putarray 3 ENTRIES ZSIZE
        putarray 4 ENTRIES SIZE
        putarray 5 ENTRIES ID
        math ENTRIES += 1
    endif
next i

for i = 0 < ENTRIES
    getarray NAME   1 i
    getarray OFFSET 2 i
    getarray ZSIZE  3 i
    getarray SIZE   4 i
    getarray ID     5 i

    getarray FNAME 6 ID
    string FNAME += /
    string FNAME += NAME

    if SIZE == ZSIZE
        log FNAME OFFSET SIZE 1
    else
        endian big
        putvarchr MEMORY_FILE SIZE 0
        log MEMORY_FILE 0 0
        append
        math LIMIT = OFFSET
        math LIMIT += ZSIZE
        for OFFSET = OFFSET < LIMIT
            goto OFFSET 1
            get CHUNK_ZSIZE short 1
            get CHUNK_SIZE short 1
            savepos OFFSET 1
            if CHUNK_ZSIZE == CHUNK_SIZE
                log MEMORY_FILE OFFSET CHUNK_SIZE 1
            else
                clog MEMORY_FILE OFFSET CHUNK_ZSIZE CHUNK_SIZE 1
            endif
            math OFFSET += CHUNK_ZSIZE
        next
        append
        log FNAME 0 SIZE MEMORY_FILE
    endif
next i
