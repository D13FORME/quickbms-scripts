# Rugby League Live 2 / Big Ant Studios (script version 0.3)
#   Don Bradman Cricket
#   maybe others using the Big Ant Studios engine
#   http://en.wikipedia.org/wiki/Big_Ant_Studios
# script for QuickBMS http://quickbms.aluigi.org

comtype lzma
endian big

getdstring TMP 4
if TMP == "M3MP"
    callfunction M3MP_UNPACK 1
    cleanexit
endif

goto 0
getdstring DUMMY 20
idstring "BLOB"
get BLOBS long
get FILES long

for b = 0 < BLOBS
    for i = 0 < FILES
        get OFFSET long
        get ZSIZE long
        get SIZE long
        get END_OFFSET long
        get XZSIZE long
        get XSIZE long
        get DUMMY long
        get BNUM long

        if SIZE == ZSIZE
            savepos TMP_OFF
            goto OFFSET
            getdstring TMP 4
            if TMP == "M3MP"
                callfunction M3MP_UNPACK 1
            else
                log "" OFFSET SIZE
            endif
            goto TMP_OFF
        else
            savepos TMP_OFFSET

            math TMP = SIZE
            math TMP += XSIZE
            putvarchr MEMORY_FILE TMP 0
            log MEMORY_FILE 0 0
            append
            callfunction UNPACK 1
            if XSIZE != 0
                callfunction UNPACK 1
            endif
            append

            get SIZE asize MEMORY_FILE
            log "" 0 SIZE MEMORY_FILE
            goto TMP_OFFSET
        endif
    next i
next b

startfunction UNPACK
    goto OFFSET
    get CHUNKS long
    for x = 0 < CHUNKS
        get CHUNK_ZSIZE long
        putarray 0 x CHUNK_ZSIZE
    next x
    savepos OFFSET

    for x = 0 < CHUNKS
        getarray CHUNK_ZSIZE 0 x
        goto OFFSET
        get CHUNK_SIZE long
        math CHUNK_ZSIZE -= 4
        savepos OFFSET
        if CHUNK_ZSIZE == CHUNK_SIZE
            log MEMORY_FILE OFFSET CHUNK_SIZE
        else
            clog MEMORY_FILE OFFSET CHUNK_ZSIZE CHUNK_SIZE
        endif
        math OFFSET += CHUNK_ZSIZE
    next x
endfunction

startfunction M3MP_UNPACK
    get M3MP_FILES long
    get M3MP_CHUNKS long
    get M3MP_DATA_OFF long
    savepos M3MP_OFF
    savepos M3MP_INFO_OFF
    math M3MP_OFF -= 0x10

    math M3MP_DATA_OFF += M3MP_OFF
    goto M3MP_DATA_OFF
    log MEMORY_FILE 0 0
    append
    for j = 0 < M3MP_CHUNKS
        get CHUNK_OFFSET long
        get CHUNK_ZSIZE long
        get CHUNK_SIZE long
        math CHUNK_OFFSET += M3MP_OFF
        if CHUNK_ZSIZE == CHUNK_SIZE
            log MEMORY_FILE CHUNK_OFFSET CHUNK_SIZE
        else
            clog MEMORY_FILE CHUNK_OFFSET CHUNK_ZSIZE CHUNK_SIZE
        endif
    next j
    append
    
    goto M3MP_INFO_OFF
    for j = 0 < M3MP_FILES
        get OFFSET long
        get SIZE long
        get NAME_OFF long
        get DUMMY long
        savepos TMP
        math NAME_OFF += M3MP_OFF
        goto NAME_OFF
        get NAME string
        goto TMP
        log NAME OFFSET SIZE MEMORY_FILE
    next j
endfunction
